# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: web.de jan.stuehler

pkgname=perl-pdf-builder
_perl_namespace=PDF
_perl_module=Builder
pkgver=3.022
pkgrel=2
pkgdesc="Facilitates the creation and modification of PDF files"
arch=('any')
url="https://metacpan.org/release/${_perl_namespace}-${_perl_module}"
license=('LGPL')
depends=('perl' 'perl-font-ttf')
checkdepends=('perl-test-exception' 'perl-test-memory-cycle')
source=("https://www.cpan.org/modules/by-module/${_perl_namespace}/${_perl_namespace}-${_perl_module}-${pkgver}.tar.gz")
sha512sums=('30c7696af07355dbcbccc331f6502652c94297974ff957126671caa7f400708fe9d4e908e6cf79c544870ece6d7ca5fd4312d0d0d70fbc32b3252571b9d8c156')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
