# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=acpica
pkgver=20221020
pkgrel=1
pkgdesc="ACPI tools, including Intel ACPI Source Language compiler"
arch=(x86_64)
url="https://acpica.org"
license=(GPL2)
depends=(glibc)
makedepends=(flex bison)
provides=(iasl)
conflicts=(iasl)
replaces=(iasl)
options=(!makeflags)
_gitcommit=28fc163aa29e208678d901d98bb9030b775521b3
source=(${pkgname}-unix-${pkgver}::"git+https://github.com/acpica/acpica#commit=${_gitcommit}")
sha256sums=('SKIP')

build() {
  cd acpica-unix-${pkgver}
  make -j$(nproc --all)
}

package() {
  cd acpica-unix-${pkgver}
  make DESTDIR="${pkgdir}" install
}
