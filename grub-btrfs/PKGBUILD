# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: gato_lento <vukk.euob at gmail dot com>
# Contributor: James W. Barnett <james at wbarnett dot us>

pkgname=grub-btrfs
pkgver=4.11
pkgrel=2
pkgdesc='Include btrfs snapshots in GRUB boot options'
arch=('any')
url="https://github.com/Antynea/${pkgname}"
license=('GPL3')
depends=('btrfs-progs' 'grub')
optdepends=('snapper: Snapper support')
backup=('etc/default/grub-btrfs/config')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('c728d382545ebf2d3fabdb6853b89176f0e78bccbed01112c985b11570b1abba')

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" INITCPIO=true install
}

# vim:set ts=4 sw=4 et:
