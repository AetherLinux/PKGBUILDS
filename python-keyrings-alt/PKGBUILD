# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgname=python-keyrings-alt
_pkgname=keyrings.alt
epoch=1
pkgver=4.1.1
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
url='https://github.com/jaraco/keyrings.alt'
license=('MIT')
depends=('python-keyring')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('libgnome-keyring' 'python-pycryptodome' 'python-gobject' 'python-keyring'
              'python-pytest-enabler' 'python-pytest-mypy')
optdepends=('libgnome-keyring: GNOME Integration'
            'python-gobject: GNOME Integration'
            'python-fs'
            'python-crypto: Encrypted file storage')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ba9f30ed30cc90a569dae76ffb4b9e7b97be0d63d5d683ab9ffc45cf866bc394b70cec3ba0bfbc3a5344c3853155b460c0fe3887a2b66a220d04270d6da746d8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  pytest
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
