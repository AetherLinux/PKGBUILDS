# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.89
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/Date-Manip/"
depends=('perl')
makedepends=('perl-file-find-rule' 'perl-test-inter' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-$pkgver.tar.gz")
sha512sums=('14e1bb9173f9895cfecf17642c70fa00e5406b875bf88115b31c12a47d1d7c132587a41ac924da6e94aa4d20de17bbe3f4d9a11b05c3ae3d57802f55fa5ad5f8')

build() {
  cd Date-Manip-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Date-Manip-$pkgver
  make test
}

package() {
  cd Date-Manip-$pkgver
  make DESTDIR="$pkgdir" install
}
