# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.3.7
pkgrel=1

pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3' 'glib2' 'json-c' 'pango')
makedepends=('meson' 'ninja' 'doxygen' 'check')

source=(girara-$pkgver.tar.gz::https://github.com/pwmt/girara/archive/$pkgver.tar.gz)

sha512sums=('19bfc1c8f5e19315a5bf0fd7ca0f60fb396af8d79663147db24564d93f4a31303d418d9c84eb11614ce8f4526ee65d15d95fa8b9c06e27f0da35e0248c6a8622')

build() {
  cd girara-$pkgver
  arch-meson build

  cd build
  ninja
}

check() {
  cd girara-$pkgver/build

  ninja test
}

package() {
  cd girara-$pkgver/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
