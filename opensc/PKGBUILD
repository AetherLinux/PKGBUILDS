# Maintainer: Alad
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: SÃ©bastien Luttringer
# Contributor: kevku <kevku@msn.com>

pkgname=opensc
pkgver=0.22.0
pkgrel=2
pkgdesc='Tools and libraries for smart cards'
arch=('x86_64')
url='https://github.com/OpenSC/OpenSC/wiki'
license=('LGPL')
backup=('etc/opensc.conf')
makedepends=('docbook-xsl')
depends=('pcsclite' 'glib2' 'libltdl')
options=(!emptydirs !lto)
source=("https://github.com/OpenSC/OpenSC/releases/download/$pkgver/opensc-$pkgver.tar.gz"
        opensc-openssl-3.patch)
sha512sums=('70ad5f1219f2ec2a5529a30d8e9955510c77c87d0bc857d5951c7227c75cf9de1c6071f43dd3816a2034123ebd902709793ff776e84d5379fdd54c811ccfaaee'
            '53cbfd98ac49c0436cc39f069cf2a099d66d337a102682fcd17031f4ffcf6ebf7625a7b7aad44170010e291b7265954bc9a8e6650df86672ce3be88c85dae659')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../opensc-openssl-3.patch
  # Remove -Werror to ignore openssl 3 deprecation warnings
  sed -i 's/-Werror//' configure.ac
}

build() {
  cd $pkgname-$pkgver

  _sheetdir=(/usr/share/xml/docbook/xsl-stylesheets-*-nons)

  ./bootstrap
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-completiondir=/usr/share/bash-completion/completions \
    --enable-man \
    --enable-doc \
    --enable-readline \
    --enable-openssl \
    --enable-pcsc \
    --enable-zlib \
    --enable-sm \
    --with-xsl-stylesheetsdir="$_sheetdir"

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package(){
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 etc/opensc.conf "$pkgdir/etc/opensc.conf"
}

# vim:set ts=2 sw=2 et:
