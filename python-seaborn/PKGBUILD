# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

# TODO: python-fastcluster is optional dependency (move from AUR)

pkgname=python-seaborn
pkgver=0.11.2
pkgrel=3
pkgdesc="Statistical data visualization"
arch=('any')
url="https://seaborn.pydata.org/"
license=('BSD')
depends=('python-pandas' 'python-matplotlib' 'python-scipy')
makedepends=('python-setuptools')
optdepends=('python-statsmodels: for some advanced statistical plots')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mwaskom/seaborn/archive/v${pkgver}.tar.gz")
sha256sums=('693e3877d7608e08201feb6b99a7c9fce06ecaa88acbb8c3e928bdff9feb8515')

build() {
  cd "$srcdir"/seaborn-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/seaborn-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
