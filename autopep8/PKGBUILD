# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=2.0.0
pkgrel=1
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
depends=('python-pycodestyle' 'python-tomli')
makedepends=('python-setuptools')
source=("https://github.com/hhatto/autopep8/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('66568e6fa97504631a28633cdbbab2dadccfbe27a78a44f3d0630b1cf4c09c29b2c2643f6640c6908edb6af5d2e94893872de63af7c1047ba8fe4c6f5fa613bb')

build() {
  cd autopep8-$pkgver
  python setup.py build
}

check() {
  cd autopep8-$pkgver
  python setup.py test || true
}

package() {
  cd autopep8-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
