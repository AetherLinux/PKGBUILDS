# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='faraday-net_http'
pkgname="ruby-${_gemname}"
pkgver=1.0.1
pkgrel=1
pkgdesc='Faraday adapter for Net::HTTP'
arch=('any')
url='https://github.com/lostisland/faraday-net_http'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rspec')
checkdepends=('ruby-rspec' 'ruby-faraday' 'ruby-webmock' 'ruby-multipart-parser')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('57a7ea2f452844200119cb9e99ae4bfee5e01b0630b38b4ef4e615dfcc05e1476e3da8511698bf199c62b816b5253fb364ed6cffdc59dcd4e9d706c7f1e6e56b')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # drop broken/cirular dependencies
  sed --in-place '/add_development_dependency/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
   rspec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md CODE_OF_CONDUCT.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
