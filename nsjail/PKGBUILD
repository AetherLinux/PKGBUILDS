# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=nsjail
pkgver=3.1
pkgrel=3
pkgdesc="Light-weight process isolation tool"
arch=(x86_64)
url="https://nsjail.com"
license=(Apache)
depends=(protobuf libnl)
makedepends=(git)
source=(https://github.com/google/nsjail/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        git+https://github.com/google/kafel.git#commit=00532cc1ee142355c2db8a58001bcc669893cff8)
sha256sums=('c944ce9b6dbfae7cc42b67ce720f997b3b12a2b41ba3462e133627a838f3ff3c'
            'SKIP')

prepare() {
  mv kafel ${pkgname}-${pkgver}
}

build() {
  make -C ${pkgname}-${pkgver}
}

package() {
  install -Dm755 ${pkgname}-${pkgver}/nsjail -t "${pkgdir}"/usr/bin/ 
}
