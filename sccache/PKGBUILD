# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=sccache
pkgver=0.3.0
pkgrel=2
pkgdesc='Shared compilation cache'
arch=(x86_64)
url=https://github.com/mozilla/sccache
license=(Apache)
depends=(
  gcc-libs
  glibc
  openssl
  zlib
)
makedepends=(
  git
  rust
)
optdepends=(
  'memcached: Memcached support'
  'redis: Redis support'
)
_tag=f6e36e68c6cb1e17127273956df9ebf2127dff1a
source=(git+https://github.com/mozilla/sccache.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd sccache
  git describe --tags | sed 's/^v//'
}

prepare() {
  cargo fetch \
    --locked \
    --manifest-path sccache/Cargo.toml
}

build() {
  export CFLAGS+=' -ffat-lto-objects'
  cargo build \
    --release \
    --frozen \
    --manifest-path sccache/Cargo.toml \
    --features all \
    --features native-zlib
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path sccache/Cargo.toml \
    --features all \
    --features native-zlib
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path sccache \
    --root "${pkgdir}"/usr \
    --features all \
    --features native-zlib
}

# vim: ts=2 sw=2 et:
