# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

pkgname=python-statsmodels
pkgver=0.13.2
pkgrel=3
pkgdesc="Statistical computations and models for use with SciPy"
arch=(x86_64)
url="https://www.statsmodels.org/"
license=(BSD)
depends=(python-numpy python-scipy python-pandas python-patsy)
makedepends=(python-setuptools cython)
optdepends=('jupyter-notebook: needed to run the notebooks'
            'python-cvxopt: regularized fitting of some models'
            'python-joblib: accelerate distributed estimation for certain models'
            'python-matplotlib: plotting functions')
checkdepends=(python-pytest python-pytest-xdist python-joblib python-matplotlib)
source=(https://github.com/statsmodels/statsmodels/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2c37244a87d9116f9e7de134219fcecee947ffe43cb66b949ee6d0422edfb4cc')

build() {
  cd statsmodels-${pkgver}
  python setup.py build
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd statsmodels-${pkgver}/build/lib.linux-${CARCH}-${python_version}
  PYTHONPATH="${PWD}" pytest -vv --color=yes statsmodels || echo "Tests failed"
}

package() {
  cd statsmodels-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
