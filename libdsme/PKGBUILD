# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libdsme
pkgver=0.66.7
pkgrel=1
pkgdesc="DSME dsmesock dynamic library"
arch=('x86_64')
url="https://github.com/sailfishos/libdsme"
license=('LGPL')
depends=('glib2')
makedepends=('check')
source=("https://github.com/sailfishos/libdsme/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        libdsme-uio.patch)
sha512sums=('c3fdd6b6206d980ba0c08c574aba19edb5a5d103a21630597dd4d900c046a08a1d3960b8c8b47e25b60997464451d51afae5449ddbc6784781f827c274538c08'
            'aba3ffda5cab681133bc2df12f91c6b572e5b11692f9485558be36a80bfbf43a6438df2bf3c003c4f6e3dc73afc345435885eedf7d6b17c0083f054008a84a33')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../libdsme-uio.patch # Fix build
}

build() {
  cd $pkgname-$pkgver
  ./verify_version
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install_main install_devel

  # Fix symlinks
  for name in libdsme libdsme_dbus_if libthermalmanager_dbus_if; do
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so.0
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so
  done
}
