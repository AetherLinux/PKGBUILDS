# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=geopandas
pkgname=python-${_pkg}
pkgver=0.11.1
pkgrel=1
pkgdesc="Python tools for geographic data"
arch=(any)
url="https://geopandas.org"
license=(BSD)
depends=(
    gdal
    python-fiona
    python-numpy
    python-packaging
    python-pandas
    python-pyproj
    python-shapely
)
optdepends=(
    'python-pygeos: alternative backend'
    'python-pyogrio: vectorized IO for GDAL/OGR'
    'python-rtree: spatial access methods'
    'python-psycopg2: PostGIS access'
    'python-sqlalchemy: other geodatabase access'
    'python-geopy: geocoding'
    'python-matplotlib: plotting'
#    'python-mapclassify: plotting'
)
makedepends=(python-setuptools)
checkdepends=(
    python-pytest
    python-fsspec
    python-pygeos
    python-pyogrio
    python-rtree
    python-matplotlib
    #python-mapclassify
    #python-folium
    #python-xyzservices
    python-scipy
    python-geopy
    python-sqlalchemy
    python-psycopg2
    libspatialite
    #python-geoalchemy2
    python-pyarrow
    postgis
    ghostscript
)
# No tests data in Pypi
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/geopandas/geopandas/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0ba3c2c56013658e321e1ff9b4d7ec45d086b3603a63476ca53a8265e699ecc5')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}"/build/lib/ pytest -vv --color=yes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
