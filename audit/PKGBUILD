# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: <kang@insecure.ws>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgbase=audit
pkgname=(audit python-audit)
pkgver=3.0.9
pkgrel=1
pkgdesc='Userspace components of the audit framework'
url='https://people.redhat.com/sgrubb/audit'
arch=(x86_64)
makedepends=(glibc krb5 libcap-ng libldap linux-headers python swig)
license=(GPL2 LGPL2.1)
options=(debug emptydirs)
source=(
  https://people.redhat.com/sgrubb/audit/$pkgname-$pkgver.tar.gz
  $pkgbase-3.0.8-config_paths.patch
  audit-3.0.8-flex-array-workaround.patch
)
sha512sums=('5219eb0b41746eca3406008a97731c0083e7be50ec88563a39537de22cb69fe88490f5fe5a11535930f360b11a62538e2ff6cbe39e059cd760038363954ef4d6'
            'bc699123f810abcf9300728bf61d7fcfcc83677b75fbeb713fb24cc11b2f9edf687661aab70766acde7c3c41c6a62f8e0a54cdfb49d1c7ce2246b67fbe3feec5'
            '09b93e1204106eaf536ccd5b1c1fa88bd34a4b39b2a5534e27020b0c9fe1b50501b044e55e84fa97dffb9037ffe8291da05a7fae61bcf52800a46e1874b6bae7')

prepare() {
  # replace the use of /sbin with /usr/bin in configs
  patch -Np1 -d $pkgbase-$pkgver -i ../$pkgbase-3.0.8-config_paths.patch
  # flex array workaround
  cp /usr/include/linux/audit.h $pkgbase-$pkgver/lib/
  patch -Np1 -d $pkgbase-$pkgver -i ../audit-3.0.8-flex-array-workaround.patch
}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/audit \
    --enable-gssapi-krb5=yes \
    --enable-systemd=yes \
    --with-libcap-ng=yes
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  [ -n "$SOURCE_DATE_EPOCH" ] && touch -h -d @$SOURCE_DATE_EPOCH bindings/swig/python/audit.py
}

package_audit() {
  depends=(glibc krb5 libkrb5.so libgssapi_krb5.so libcap-ng libcap-ng.so)
  provides=(libaudit.so libauparse.so)
  backup=(
    etc/libaudit.conf
    etc/audit/audit-stop.rules
    etc/audit/auditd.conf
    etc/audit/audisp-remote.conf
    etc/audit/zos-remote.conf
    etc/audit/plugins.d/af_unix.conf
    etc/audit/plugins.d/au-remote.conf
    etc/audit/plugins.d/audispd-zos-remote.conf
    etc/audit/plugins.d/syslog.conf
  )

  make DESTDIR="$pkgdir" install -C $pkgbase-$pkgver

  # add log dir
  install -vdm 700 "$pkgdir/var/log/$pkgname"

  # remove legacy files
  rm -frv "$pkgdir/usr/lib/audit"

  # remove files provided by python-audit
  rm -frv "$pkgdir/usr/lib/python"*
}

package_python-audit() {
  depends=(audit libaudit.so libauparse.so glibc python)
  pkgdesc+=' (python bindings)'

  make DESTDIR="$pkgdir" install -C $pkgbase-$pkgver/bindings
}

# vim: ts=2 sw=2 et:
