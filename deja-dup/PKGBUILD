# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=43.4
pkgrel=1
pkgdesc='A trivial-to-use personal backup tool to protect against accidental data loss'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/DejaDup'
license=('GPL')
depends=(
  'duplicity'
  'gvfs'
  'libsecret'
  'org.freedesktop.secrets'
  'python-gobject'
  'python-pydrive2'
  'libadwaita'
)
makedepends=(
  'appstream-glib'
  'itstool'
  'meson'
  'vala'
)
source=("https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f7ea87c500953e81d8d000c71e222e29f44adcb6749fcfaba6b6c5a1e71dd03d5a6dbf0206feb5b8d266739de4f021b251eb9279123a8f001941143e764642f3')
b2sums=('ab856ec1e0d1edcecc2823903c5af4af7d38a7e4fd5be889c49d10bf717509e31ebbf67b87cd2fca8a0c50d22573ce6c5833e80d9f87be068b04139fb2c32614')
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry <mike@mterry.name>

prepare() {
  find "$pkgname-$pkgver" -name meson.build | xargs sed -e 's|C.UTF-8|C|' -i
}

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
