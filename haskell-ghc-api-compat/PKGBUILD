# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-api-compat
pkgname=haskell-ghc-api-compat
pkgver=8.6.1
_commit=9e8c1ec540bc0a68700e8541d44dca14913c5c9e
pkgrel=6
pkgdesc="GHC-API compatibility helpers"
url="https://hackage.haskell.org/package/ghc-api-compat"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc')
makedepends=('ghc' 'uusi')
# https://gitlab.haskell.org/haskell/ghc-api-compat/-/issues/1
#source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
source=("https://gitlab.haskell.org/haskell/ghc-api-compat/-/archive/$_commit/ghc-api-compat-$_commit.tar.bz2"
        # https://gitlab.haskell.org/haskell/ghc-api-compat/-/merge_requests/2.patch
        $_hkgname-ways.patch)
sha256sums=('c9ff586aca0ccd6ccffe333403d21c9b86ed6dbd6335830419c0875c7df891bc'
            '66463c2aafb9e341c5bd3d15bc21b7794b25e3ae231bf4c809f78bf24fca3e8d')

prepare() {
  cd $_hkgname-$_commit
  gen-setup
  # sed -i 's/GHC.Platform.ARM64/GHC.Platform.AArch64/' $_hkgname.cabal
  patch -p1 -i ../$_hkgname-ways.patch
}

build() {
  cd $_hkgname-$_commit

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$_commit

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
