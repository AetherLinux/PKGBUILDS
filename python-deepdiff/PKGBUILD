# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deepdiff
pkgver=5.8.2
pkgrel=1
pkgdesc="Deep Difference and Search of any Python object/data."
url="https://github.com/seperman/deepdiff"
license=('MIT')
arch=('any')
depends=('python-ordered-set')
optdepends=('python-click: for cli'
            'python-yaml: for cli'
            'python-toml: for cli'
            'python-clevercsv: for cli')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-clevercsv' 'python-click' 'python-jsonpickle' 'python-numpy'
              'python-toml' 'python-yaml')
source=("https://github.com/seperman/deepdiff/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('20b812708a5d900cec9ad16c661b0b5ada755e607b258f73a4f9757d06d9e9ea2fa3988a54a5cc071ae2de6585e48fefd1cdd5896edca0100a4a81ca13d5452c')

build() {
  cd deepdiff-$pkgver
  python setup.py build
}

check() {
  cd deepdiff-$pkgver
  pytest tests
}

package() {
  cd deepdiff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
