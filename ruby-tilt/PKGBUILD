# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=tilt
pkgname=ruby-tilt
pkgver=2.0.10
pkgrel=1
pkgdesc='Generic interface to multiple Ruby template engines'
url='https://github.com/rtomayko/tilt'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-ronn' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rtomayko/tilt/archive/v${pkgver}.tar.gz)
sha256sums=('b642bdb142e6d5e42d381d25e9ba2c4c9cc1cdf0071433bbf465df97e6341d2e')
sha512sums=('4a36c55347e307ea0c842aad3da72cebe98bcf8a0421040cfce7a43540d1746e736ce92b7ce7492a5cfc184372842c3ac7a006f3650b4c9420d5825ec005b4ef')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
  ronn -w -s toc -r5 --markdown man/*.ronn
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md man/tilt.1.{markdown,html} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 man/tilt.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
