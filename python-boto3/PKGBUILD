# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=python-boto3
pkgver=1.24.96
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
depends=('python' 'python-botocore' 'python-jmespath' 'python-s3transfer')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('9c3ac6dba99904a54e4b06b02531c2388d235b1926cb5bd7b767c679632b613db4a0d453030295e5f4101efdb7a3df8aca08e3c2e92a6725dc3df733cef37d72')

build() {
  cd boto3-$pkgver
  python setup.py build
}

check() {
  cd boto3-$pkgver
  # Many integration tests need real credentials
  pytest tests -n auto --ignore=tests/integration
}

package() {
  cd boto3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
