# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-clevercsv
pkgver=0.7.2
pkgrel=1
pkgdesc="A Python package for handling messy CSV files"
url="https://github.com/alan-turing-institute/CleverCSV"
license=('MIT')
arch=('x86_64')
depends=('python-chardet' 'python-regex')
makedepends=('python-setuptools')
optdepends=('python-pandas' 'python-tabview' 'python-wilderness' 'python-cchardet')
checkdepends=('python-pytest-runner' 'python-pandas' 'python-tabview' 'python-wilderness'
              'python-cchardet' 'python-termcolor')
source=("https://github.com/alan-turing-institute/CleverCSV/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3d04a309a1cd27d0285efc60bea44b7ce874760705209ba9f3757d3bdc37019824eb4a9bae6e6a99df10df89afadaa7399296b6b8fa95aede92a68d4c0a0014a')

build() {
  cd CleverCSV-$pkgver
  python setup.py build
}

check() {
  cd CleverCSV-$pkgver
  PYTHONPATH="$PWD"/build/lib.linux-$CARCH-3.10 python setup.py pytest
}

package() {
  cd CleverCSV-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  mkdir "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man 

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
