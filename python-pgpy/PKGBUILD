# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: James Morris <jmorris@securityinnovation.com>

pkgname=python-pgpy
pkgver=0.5.4
pkgrel=3
pkgdesc='Pretty Good Privacy for Python'
arch=('any')
license=('BSD')
url='https://github.com/SecurityInnovation/PGPy'
depends=(
  'python'
  'python-cryptography'
  'python-six'
  'python-pyasn1'
)
makedepends=(
  'git'
  'python-setuptools'
)
checkdepends=(
  'python-pytest'
  'python-pytest-order'
)
_commit='345f4b8d74f4123b97198e10a383f485f80946d8'
source=(
  "$pkgname::git+$url.git#commit=$_commit"
  'idea-deprecation.patch'
)
b2sums=('SKIP'
        '55917c5f5e17aeea00c879e9c1329a5e28add956e87fb4e6159119fa7a2a4793f788851cbe716258acf34c9ffd6524f44273fe599329354a22b990ddc768dbf2')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # https://github.com/SecurityInnovation/PGPy/pull/378
  sed -i '/wheel/d' setup.cfg

  # https://github.com/SecurityInnovation/PGPy/pull/373
  git cherry-pick --no-commit e2f427ea5c0347fb5995ed2a9bc00f780c212c70

  # https://github.com/SecurityInnovation/PGPy/pull/403
  git cherry-pick --no-commit d84597eb8417a482433ff51dc6b13060d4b2e686
  patch -p1 -i "$srcdir/idea-deprecation.patch"
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  pytest
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
