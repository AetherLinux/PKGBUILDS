# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fulcio
pkgver=0.5.3
pkgrel=1
pkgdesc="free Root-CA for code signing certs - issuing certificates based on an OIDC email address."
arch=('x86_64')
url="https://github.com/sigstore/fulcio"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/fulcio/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('0f7f2cb83695be489c05e1a9bd510897088a1aab968e7bcc9869c2bb07e7155f6787dbcd0c1af1581aa11ad3801a2680e73dc328515b909c0c96e81747381cea')
b2sums=('0b931e334cfc6e60c1bb71d9efe1ac540dd192ed1fd251fdfd9685216f2ad8d75e8f79dc59a0fa9060523a3b9935686a450f2657e78a2ca3624b1986c40a4f4a')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/fulcio "${pkgdir}/usr/bin/${pkgname}"
}
