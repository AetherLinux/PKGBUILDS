# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.3.1
pkgrel=2
pkgdesc="A spatial database extender for PostgreSQL"
arch=(x86_64)
url="https://postgis.net/"
license=(GPL)
depends=(postgresql gdal geos json-c libxml2 proj protobuf-c pcre2)
optdepends=('perl: for contrib script postgis_restore.pl')
makedepends=(clang llvm)
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('91be800a72d748c5a3a4a00d82ac1de42023e29da61ece6ebf9c77fe228fcb1a')
b2sums=('b24bbddae8ecad21525d3542cce409803b5e4f33e18d17d1e5b6cd04d99b64340720d9e60602af2cce6f6c9636c9943d6a14d5a2e34b0d5c32f6e3d87e6590f8')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
