# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='faraday-patron'
pkgname="ruby-${_gemname}"
pkgver=1.0.0
_archivepkgver=1.0
pkgrel=1
pkgdesc='About Faraday adapter for Patron'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rspec')
checkdepends=('ruby-faraday' 'ruby-webmock' 'ruby-multipart-parser')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${_archivepkgver}.tar.gz")
sha512sums=('66180ab2730280635a2560839e991e562bb6ff907e483a6e75557f6d7950b0b375955eea4034c8c4820267ac82d9f9dc0f0011c4ff6a526f3499b179935a7014')

prepare() {
  cd "${srcdir}/${_gemname}-${_archivepkgver}"

  # drop broken/cirular dependencies
  sed --in-place '/add_development_dependency/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${_archivepkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${_archivepkgver}"
  # tests don't yet pass because patron is missing in the repos
  # we run the tests in ruby-faraday
  #rspec
}

package() {
cd "${srcdir}/${_gemname}-${_archivepkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
