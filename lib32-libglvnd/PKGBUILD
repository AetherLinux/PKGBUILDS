# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-libglvnd
pkgver=1.5.0
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/glvnd/libglvnd"
license=('custom:BSD-like')
makedepends=('lib32-libx11' 'lib32-libxext' 'xorgproto' 'python' 'meson')
provides=('lib32-libgl' 'lib32-libegl' 'lib32-libgles')
provides+=(libEGL.so libGL.so libGLESv2.so libGLX.so libGLdispatch.so libOpenGL.so)
options=('debug')
source=("https://gitlab.freedesktop.org/glvnd/libglvnd/-/archive/v$pkgver/libglvnd-v$pkgver.tar.gz")
sha512sums=('e70f98b644e7afabba77890e46278503b60158bfa58283f31e0e686dd13eb6b159ffe8ac0d1b8d3deeabff9abc00a431970b77b9731ebcf9a224c2da379823cb')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  arch-meson libglvnd-v$pkgver build \
    --libdir=/usr/lib32 \
    -D headers=false \
    -D gles1=false

  meson compile -C build
}

package() {
  # lib32-libglvnd needs lib32-mesa for indirect rendering
  depends=('lib32-libxext' 'libglvnd' 'lib32-mesa' 'lib32-opengl-driver')

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/include

  mkdir -p "$pkgdir"/usr/share/licenses
  ln -s libglvnd "$pkgdir"/usr/share/licenses/$pkgname
}
