# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='faraday-multipart'
pkgname="ruby-${_gemname}"
pkgver=1.0.3
pkgrel=1
pkgdesc='Perform multipart-post requests using Faraday'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=('ruby' 'ruby-multipart-post')
makedepends=('ruby-rspec')
checkdepends=('ruby-faraday' 'ruby-webmock' 'ruby-multipart-parser' 'ruby-rack-test')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('efe09589db1cad497ef832f11e0d22042bb020271269a3f82818cfe5dee6ebee624db6b9fe0e5a1e98c0ac5088638b8b5d0ab9c0407158b8919f0ebbdfe66131')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rspec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
