# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-rpyc
_name=rpyc
pkgver=5.2.3
pkgrel=1
pkgdesc='Remote Python Call - A transparent and symmetric RPC library for python'
url='https://github.com/tomerfiliba-org/rpyc'
arch=('any')
license=('MIT')
depends=('python' 'python-plumbum')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
source=(https://github.com/tomerfiliba-org/${_name}/archive/${pkgver}/${_name}-${pkgver}.tar.gz)
sha512sums=('4be189a719be100886e08d9c2c0769d12b7d1c5e167c197ebee1a3db542e228f2124fea976e5af3680136b298ce4632c79b2f302128dff31ebf45dcf52a9743f')
b2sums=('3c03d587293375f875de31d6a89ba07b47bd4f4ff9fd5c7b23ed0c83e420e9d2bab08844c3538299797203ac90e7e81b955db02de70bf1cee5ea59d8475a6848')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
