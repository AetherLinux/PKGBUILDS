# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=ostree
_commit=73744a3a4dfc7aa9ab183a9700273eee2f49750a  # tags/v2022.6
pkgver=2022.6
pkgrel=4
pkgdesc="Operating system and container binary deployment and upgrades"
url="https://ostreedev.github.io/ostree/"
arch=(x86_64)
license=(GPL)
depends=(fuse3 glibc libgpg-error libsodium util-linux openssl xz zlib curl)
makedepends=(avahi e2fsprogs git glib2 gobject-introspection gpgme gtk-doc
             libarchive libxslt mkinitcpio python systemd)
checkdepends=(cpio elfutils gjs parallel python-yaml syslinux)
options=(debug)
source=(
  git+https://github.com/ostreedev/ostree#tag=$_commit
  git+https://github.com/mendsley/bsdiff
  git+https://gitlab.gnome.org/GNOME/libglnx.git
  ostree-2022.6-use_fuse3.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        '82a8f557f4f21a0a769709c8e2ffb9165a8f3ad60e2d9b6722f3d06a34b102a22cd137f77530e6dc9d84a9dcdf429c5907653022f07f368c2bb2703711aab435')
# upstream ticket about chain of trust:
# https://github.com/ostreedev/ostree/issues/2349
validpgpkeys=('1CEC7A9DF7DA85ABEF843DC0A866D7CCAE087291') # Colin Walters <walters@redhat.com>

prepare() {
  cd ostree

  # use fusemount3 (fuse3)
  git apply -3 ../ostree-2022.6-use_fuse3.patch

  git submodule init
  git submodule set-url bsdiff "$srcdir/bsdiff"
  git submodule set-url libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --sbindir=/usr/bin
    --libexecdir=/usr/lib
    --disable-static
    --enable-experimental-api
    --enable-gtk-doc
    --with-builtin-grub2-mkconfig
    --with-curl
    --with-dracut
    --with-ed25519-libsodium
    --with-mkinitcpio
    --with-openssl
    --without-soup
  )

  cd ostree
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make check -k -C ostree || echo "Known to fail: https://github.com/ostreedev/ostree/issues/2313"
}

package() {
  depends+=(libarchive.so libavahi-client.so libavahi-common.so
            libavahi-glib.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so
            libgpgme.so libsystemd.so)
  provides=(libostree-1.so)

  make DESTDIR="$pkgdir" install -C ostree
}
