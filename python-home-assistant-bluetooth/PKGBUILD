# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-home-assistant-bluetooth
pkgdesc='Basic bluetooth models used by Home Assistant'
pkgver=1.6.0
pkgrel=1
arch=(any)
url=https://github.com/home-assistant-libs/home-assistant-bluetooth
license=(APACHE)
depends=(
  python
  python-bleak
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
_tag=6733b02bbaaa88e3e1c9c958218914cd6a72b060
source=(git+https://github.com/home-assistant-libs/home-assistant-bluetooth.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd home-assistant-bluetooth
  git describe --tags | sed 's/^v//'
}

build() {
  cd home-assistant-bluetooth
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" home-assistant-bluetooth/dist/*.whl
}

# vim: ts=2 sw=2 et:
