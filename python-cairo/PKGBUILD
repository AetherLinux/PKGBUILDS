# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=python-cairo
pkgver=1.22.0
pkgrel=1
pkgdesc="Python bindings for the cairo graphics library"
arch=('x86_64')
url="https://pycairo.readthedocs.io/en/latest/"
license=('LGPL2.1' 'MPL')
depends=(cairo python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(https://github.com/pygobject/pycairo/releases/download/v$pkgver/pycairo-$pkgver.tar.gz{,.sig})
sha256sums=('b34517abdf619d4c7f0274f012b398d9b03bab7adc3efd2912bf36be3f911f3f'
            'SKIP')
validpgpkeys=(0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC) # Christoph Reiter <reiter.christoph@gmail.com>

build() {
  cd pycairo-$pkgver
  python setup.py build
}

check() {
  cd pycairo-$pkgver
  python setup.py test
}

package() {
  cd pycairo-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
