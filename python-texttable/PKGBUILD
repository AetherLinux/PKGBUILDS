# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgname=python-texttable
pkgver=1.6.5
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
depends=('python')
optdepends=('python-cjkwrap: for better CJK support')
makedepends=('python-cjkwrap' 'python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/foutaise/texttable/archive/v$pkgver.tar.gz")
sha512sums=('63c73cd85be0432d05abde31ef7d7c91efa461b0f7daae3c6f9a29c116771841bbc1b4daf47c8863e32ed001417b96804ece1e976595c527ee5a1f253129028c')

build() {
  cd texttable-$pkgver
  python setup.py build
}

check() {
  cd texttable-$pkgver
  pytest tests.py
}

package() {
  cd texttable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
