# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='rack-test'
pkgname="ruby-${_gemname}"
pkgver=1.1.0
pkgrel=4
pkgdesc='small, simple testing API for Rack apps'
arch=('any')
url='https://github.com/rack/rack-test'
license=('MIT')
depends=('ruby' 'ruby-rack')
makedepends=('ruby-rdoc' 'ruby-rake')
checkdepends=('ruby-rspec' 'ruby-sinatra')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz" 'disable-rubocop.patch' 'disable-simplecov.patch' 'disable-broken-test.patch')
sha512sums=('36d947d9b13d33ee8322e5b9d18fe8266787ccb7703f1fafadc040123e34dd44e98e7faf8b6dd816e51ed167065b2ffe79e9bcee74564dc324ba2b7dba76f82a'
            'b1f09db04057846e7437b09a10ba38c792cbbbfd7c9fe938237d922ef72c02e0078f6a9542532b41b2f968680d539f766646995dce0a1d11ec7ab1673bc86490'
            '5bc69c8b560bb4078e62db781698603f3f5a5722c8c1cf9912defed4862c42012db91945d852ff1ca14579f1d2b30a0978d523c35e366171542dc8afa7ef6280'
            '09925db54171a46483ca1f823a2f60a375faec175dd1d91c312260316ee1b9a615d971bc5833c4007458ea576069f883e5280cd8f0cfc22eb9c8e05008c839f5')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"

  # drop rubocop. We don't have it in the repos and it doesn't add any value during builds
  patch -p1 -N -i "${srcdir}/disable-rubocop.patch"
  # drop coverage reporting. We don't have simplecov in our repos
  patch -p1 -N -i "${srcdir}/disable-simplecov.patch"
  # we built based on an archive, not a git repo
  sed --in-place 's/git ls-files/find/' "${_gemname}.gemspec"
  # disable one test that also fails upstream
  # https://travis-ci.org/github/rack/rack-test/jobs/751430232#L346
  patch -p1 -N -i "${srcdir}/disable-broken-test.patch"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake spec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 MIT-LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md History.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
