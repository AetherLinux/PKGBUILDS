# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-lib-parser
pkgname=haskell-ghc-lib-parser
pkgver=8.10.5.20210606
pkgrel=2
pkgdesc="The GHC API, decoupled from GHC versions"
url="https://github.com/digital-asset/ghc-lib"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'alex' 'happy' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('8ad82f79254e62b8d577137b6b9506b151ffdb62be012e4d5c8fe33d13c11a5f')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup

  sed -i 's/hiding (group)/hiding (group, singleton)/' compiler/utils/Util.hs
  sed -i 's/import Data.List/import Data.List hiding (singleton)/' compiler/specialise/Rules.hs
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
