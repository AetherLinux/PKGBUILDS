# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.9.2
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="https://github.com/zopefoundation/persistent/"
depends=('python-cffi' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("https://github.com/zopefoundation/persistent/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('feb1704eaf5308a483a09e81dd94320dfeb40d4c4a2241a83b3a9cf4889c1f2d1e3e57814dd213ddfada59c7d614768f13f699d2183012ab6947cd959ab22756')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=src
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
