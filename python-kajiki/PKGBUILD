# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
pkgbase='python-kajiki'
_pkgbase="${pkgbase#python-}"
pkgname=("python-${_pkgbase}")
pkgver=0.9.1
pkgrel=1
pkgdesc='Python XML-based template engine with Genshi-like syntax and Jinja-style blocks'
arch=('any')
url='https://github.com/nandoflorestan/kajiki'
license=('LGPL')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-babel' 'python-nine' 'python-pytest')
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/nandoflorestan/kajiki/archive/v${pkgver}.tar.gz")
sha512sums=('3903e0766535939fbb2637da6138925ceeb799e90cbbbaa98005f9a8b2fd96e25525ac271ede1d9b7cad2d2f2743dc76df249ddb8a2e4639559f1648abd6d1b0')

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  export PYTHONPATH="${PWD}"
  py.test -vvv -s -ra --showlocals --noconftest tests/
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py install --root="$pkgdir/" --optimize=1
}
