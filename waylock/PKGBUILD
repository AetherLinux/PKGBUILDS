# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=waylock
pkgver=0.3.5
_commit=20f1f5c5b0349eeb0a8d5d98bba81245b84cc16e  # refs/tags/v0.3.5^{}
pkgrel=1
pkgdesc="A simple screenlocker for wayland compositors"
arch=(x86_64)
url="https://github.com/ifreund/waylock"
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(git libxkbcommon pam rust)
source=(git+https://github.com/ifreund/waylock#commit=$_commit?signed)
sha256sums=('SKIP')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <ifreund@ifreund.xyz>

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname
  cargo build --frozen --release --all-features --target-dir=target
}

check() {
  cd $pkgname
  cargo test --release --locked --target-dir=target
}

package() {
  depends+=(libpam.so libxkbcommon.so)

  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {README.md,waylock.toml} -t "$pkgdir/usr/share/doc/$pkgname/"
}
