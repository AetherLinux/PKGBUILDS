# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=3.0.0
pkgrel=1
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
checkdepends=('python-hacking' 'python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-octaviaclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b0e8e7505245ed3b3881a4a1369e80630019f1543289c5cf09f9f4f3c9677442af69aff4f764f520d3e41db0fcdf099071defe057d2100b8afca1c3c64e66130')

export PBR_VERSION=$pkgver

build() {
  cd python-octaviaclient-$pkgver
  python setup.py build
}

check() {
  cd python-octaviaclient-$pkgver
  stestr run
}

package() {
  cd python-octaviaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
