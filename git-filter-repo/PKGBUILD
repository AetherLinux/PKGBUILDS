# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=git-filter-repo
pkgver=2.34.0
pkgrel=1
pkgdesc="Quickly rewrite git repository history (filter-branch replacement)"
arch=("any")
url="https://github.com/newren/git-filter-repo"
license=("MIT")
depends=("git" "python")
makedepends=("python-setuptools-scm")
checkdepends=('rsync')
source=(
	"$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
)
sha256sums=('b1bf46af1e6a91a54056d0254e480803db8e40f631336c559a1a94d2a08389c4')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

check() {
	cd "$pkgname-$pkgver"
	./t/run_tests
}

package() {
	cd "$pkgname-$pkgver/release"
	python setup.py install --root="$pkgdir" --optimize=1

	cd ..
	install -Dm644 Documentation/man1/git-filter-repo.1 "$pkgdir/usr/share/man/man1/git-filter-repo.1"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING COPYING.mit
}
