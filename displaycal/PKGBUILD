# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: foxbunny <bg.branko@gmail.com>

_pkgname=DisplayCAL
pkgname=displaycal
pkgver=3.9.6
pkgrel=2
pkgdesc="Open Source Display Calibration and Characterization powered by Argyll CMS (Formerly known as dispcalGUI)"
arch=(x86_64)
url="https://github.com/eoyilmaz/displaycal-py3"
license=(GPL3)
depends=(argyllcms dbus dbus-python glib2 python-certifi python-distro
         python-numpy python-pillow python-send2trash python-wxpython
         python-zeroconf)
makedepends=(libxrandr libxxf86vm python-build python-installer
             python-setuptools python-wheel)
optdepends=('gnome-color-manager: profile installation for GNOME'
            'colord-kde: profile installation for KDE Plasma')
source=(${url}/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.gz
        ${pkgname}-wxfixes-py3.10.patch::https://github.com/eoyilmaz/displaycal-py3/pull/138.patch)
sha256sums=('fad0a1773f50b9a6b1c1a588e8418b5376880ff1e87672367764e4975410cb6b'
            '5ec60779038337c4d048762cb1698dd56544ad6f6fd9340011302217d7112c5c')

prepare() {
  patch -p1 -d ${_pkgname}-${pkgver} < ${pkgname}-wxfixes-py3.10.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  # https://github.com/eoyilmaz/displaycal-py3/issues/117
  install -d "${pkgdir}"/etc/xdg/autostart
  mv "${pkgdir}"/{usr/lib/python3.10/site-packages/build/.config,etc/xdg}/autostart/z-displaycal-apply-profiles.desktop
  rmdir "${pkgdir}"/usr/lib/python3.10/site-packages/build/{.config{/autostart,},}
}
