# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pigar
pkgver=1.0.2
pkgrel=2
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
depends=('jupyter-nbformat' 'python-colorama' 'python-requests' 'python-packaging')
makedepends=('python-setuptools')
source=("https://github.com/Damnever/pigar/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f91d5b20176ec47a7b45f2785471ea5e6e79a4f315c22733e6a91c7805e36bc34409c322fab4b7b4c18b57134482ccdf6b8b5e02445c5d326f41216648799322')

build() {
  cd pigar-$pkgver
  python setup.py build
}

check() {
  cd pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v
}

package() {
  cd pigar-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
