# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Luis Martinez <luis dot martinez at tuta dot io>
# Contributor: Michal Wojdyla < micwoj9292 at gmail dot com >
# Contributor: arno <apithon.AT.free.DOT.fr>
# Contributor: Pierre Bourdon <delroth@gmail.com>
# Contributor: ianux <ianux AT free DOT fr>

pkgname=python-hachoir
pkgver=3.1.3
pkgrel=1
pkgdesc='A Python library to view and edit a binary stream field by field'
arch=('any')
url='https://hachoir.readthedocs.io/'
license=('GPL')
depends=(
  'python'
  'python-urwid'
  'python-wxpython'
  'python-darkdetect'
)
makedepends=('git' 'python-setuptools')
_commit='a41e1df9ead5f6df30d616623e26b0537963ce87'
source=("$pkgname::git+https://github.com/vstinner/hachoir.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  python runtests.py
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
