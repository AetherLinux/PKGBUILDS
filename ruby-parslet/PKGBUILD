# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='parslet'
pkgname="ruby-${_gemname}"
# 2.0.0 is available on rubygems, but not Github
# https://github.com/kschiess/parslet/issues/217
pkgver=1.8.2
pkgrel=1
pkgdesc='Parser construction library with great error reporting in Ruby'
arch=('any')
url='http://kschiess.github.io/parslet/'
license=('MIT')
makedepends=('ruby-rspec' 'ruby-rdoc')
depends=('ruby')
source=("https://github.com/kschiess/parslet/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
options=("!emptydirs")
sha512sums=('7b6ccfcb7651c7844241a99747a97c822facf1fd84cf2b680cb044623d600a5bd10ddf5db76ab25208b18949bc2041306740931da4327934b241a5b1f706ca20')

prepare() {
  cd "${_gemname}-${pkgver}"
}

build() {
  cd "${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"
  # tests are disabled at the moment because ruby-ae/ruby-qed are currently missing in the repos
  #rspec
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 README HISTORY.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv example "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
