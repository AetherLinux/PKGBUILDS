# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Ordoban <dirk.langer@vvovgonik.de>

pkgname=perl-cpanel-json-xs
_perl_module=Cpanel-JSON-XS
pkgver=4.29
pkgrel=2
pkgdesc="cPanel fork of JSON::XS, fast and correct serializing"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
depends=('perl')
url="https://metacpan.org/release/${_perl_module}"
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/${_perl_module}-${pkgver}.tar.gz")
sha512sums=('5b25b76efd62b6bd943d27603dc4372d42c01477ec8121cc78767b42252548a8fa279074c072166e6bf5ae33d328f22b3c2646b9b6c36e82f84bef2850e61026')

build() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
