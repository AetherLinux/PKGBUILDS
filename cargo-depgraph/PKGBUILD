# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-depgraph
pkgver=1.3.0
pkgrel=1
pkgdesc="Creates dependency graphs for cargo projects using cargo metadata and graphviz"
arch=('x86_64')
url="https://sr.ht/~jplatte/cargo-depgraph"
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~jplatte/$pkgname/archive/v$pkgver.tar.gz"
        "update-cargo-lock.patch")
sha256sums=('63e447c1040c642258b94915b209e57380802a59d10ebd8ddc98c0650391d661'
            'c14997dbf9531456b52cc85768ec06e7de48056db811a921fc190137e8eb84e0')

prepare() {
  cd "$pkgname-v$pkgver"
  patch -Np1 -i "$srcdir/update-cargo-lock.patch"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-v$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
